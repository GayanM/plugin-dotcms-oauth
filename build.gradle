apply plugin: 'osgi'
apply plugin: 'eclipse'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-eclipse'

sourceCompatibility = '1.8'
version = '0.2'


repositories {
	maven {
        url "http://repo.dotcms.com/artifactory/libs-release"
    }
}

dependencies {
	compile('com.dotcms:dotcms:3.5.1') { transitive = true }
	provided('javax.servlet:servlet-api:2.5')
}

buildscript {
	repositories {
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
	}
}

jar {
    manifest {
        name = 'OAuth Single Signon'
        symbolicName = 'com.dotcms.osgi.oauth'
        instruction '-includeresource', 'libs'
        instruction 'Bundle-Vendor', 'dotcms'
        instruction 'Export-Package', 'com.dotcms.osgi.oauth.*'

        instruction 'Bundle-Description', 'OAuth Single Sign-On'
        instruction 'Bundle-DocURL', 'http://dotcms.com'
        instruction 'Bundle-Activator', 'com.dotcms.osgi.oauth.Activator'
        instruction 'DynamicImport-Package', '*'
        instruction 'Import-Package', 'org.scribe.builder.api.*;version=0,org.scribe.builder.*;version=0,!org.apache.commons.codec.binary.*,*;version=0'
    }
}

task removeVersionsOnManifest (dependsOn: ['jar']) << {
	def manifest1=new java.util.jar.Manifest(new FileInputStream('build/tmp/jar/MANIFEST.MF'))
	def imports = manifest1.getMainAttributes().find { it.key.toString()=="Import-Package" }
	imports.value=imports.value.toString().replaceAll(~/;version="[\(\[][0-9\.]+,[0-9\.]+[\)\]]"/,'')
	manifest1.write(new FileOutputStream("build/tmp/jar/MANIFEST_NOVERSIONS.MF"))
}


task osgijar(type: Jar, dependsOn: ['removeVersionsOnManifest'] ) { 
    appendix = "generatedjar" 
    from zipTree(jar.archivePath) // add original content 
    manifest {
    	from('build/tmp/jar/MANIFEST_NOVERSIONS.MF')
    } 
} 

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
